#include "arch/memory.h"


ENTRY(_os_startup)
GROUP(libtarget.a libgcc.a)

MEMORY {
	ram		: ORIGIN = VIRT_ADDR + TEXT_OFFSET, LENGTH = 0xFFFFFFFF
#define	rom ram
	highvectors	: ORIGIN = 0xFFFF0000, LENGTH = 0x00010000
}

SECTIONS
{

	.init : {
			*(.init.text)

		PROVIDE(__INITCALL_LIST__ = .);
			KEEP(*(SORT(.initcall*)))
			LONG(0);
	} > rom

	.vectors :  {
		PROVIDE(__vectors_start = LOADADDR(.vectors));
		KEEP(*(.vectors))
		PROVIDE(__vectors_end   = LOADADDR(.vectors) + SIZEOF(.vectors));
    	} > highvectors AT > rom

	. = LOADADDR(.vectors) + SIZEOF(.vectors);
	.text ALIGN(0x1000) : {
			*(.text*)
	} > rom

	.rodata : {
			*(.rodata*)
	} > rom

	.data : {
			*(.data*)
	} > ram AT> rom

	.bss : {
		PROVIDE(__bss_start = .);
			*(.init.task)
			*(.bss*)
			*(COMMON)
		PROVIDE(__bss_end = .);
	} > ram
}
